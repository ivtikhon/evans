#
# Simple calculator in Evans
# Copyright (c) 2019 Igor Tikhonin
#
# Model description:
# =================
# Calculator consists of the following parts:
# - stack,
# - arithmetic unit (AU),
# - display,
# - keyboard.
# Operations:
# 1) when a digit is entered, its value is pushed to the stack and displayed;
# 2) when an operation (+,-,/,*) is entered, then:
#    operation is stored to the AU, or, if the AU store is not empty,
#    the early entered operation is executed the same way as if the equal sign
#    is entered; then the current operation is stored to the AU;
# 3) when the equal sign ('=') sign is entered, the operation stored in AU
#    is executed, using the stack and the register values as operands, and
#    the result stored to register; the result is also copied to the stack
#    and displayed; the AU store is cleared.


class stack { # attributes and state variables share the same namespace within class
  attr:
    list s; # uninitialized list is empty
  state:
    last_key_domain last_key = digit;  # we don't have to refer to the domain in the assignment,
                                  # like key_domain.digit because we can recover the domain
                                  # name from the variable declaration
    bool dec_point, changed = false;
    bool tobe_cleaned; # bool variables are initialized to false by default

  dom:
    last_key_domain { digit, point } # here we define a state domain

  init:
    stack() {  # constructor has full access to all attributes
      s.append('0');
      tobe_cleaned = false;  # false is the default value, i.e. initialization here is redundant
    }

  func:
    push_key(key k) { # functions have access to attributes, but not to the state variables
      s.append(k.get_value());
    }

    push_reg(alu a) {
      s.extend(str(a.get_reg()).split());
    }

    get_value():str { ret ''.join(s); }

    clean(){ s.clear(); }

  pred: # predicates can't modify class states; predicates always return bool
        # TODO: think about variable declaration inside predicates
    is_last_key_digit(){ ret (last_key == digit); }
    is_last_key_point(){ ret (last_key == point); }
    is_dec_point(){ ret dec_point; }

  oper:
    push (key k) {
      when: # precondition and effect sections have access to states, and no access to attributes
        !changed &&
        !k.processed &&
        (k.is_digit() || (k.k_val == point && !dec_point))
      eff:
        changed = true;
        k.processed = true;
        if (k.is_point()) {
          last_key = point;
          dec_point = true;
        } else {
          last_key = digit;
        }
      exec: # exec has full access to attributes and read-only access to state variables
            # exec can call functions, but not predicates; the implication is that within class
            # namespace for attributes and states is flat;
        push_key(k);
    }

    clean(key k){
      when:
        !k.processed &&
        (k.is_digit() || k.is_point()) &&
        tobe_cleaned
      eff:
        tobe_cleaned = false;
        dec_point = false;
        last_key = digit;
      exec:
        clean();
    }

    copy_from_register(key k, alu a){
      when:
        a.is_op_executed &&
        (k.is_op() || k.is_eq()) &&
        !k.processed
      eff:
        if (k.is_eq()) {
          k.processed = true;
          changed = true;
          a.reg_stored = false;
          tobe_cleaned = true;
          a.op_executed = false;
        }
      exec:
        clean();
        push_reg(a);
    }
}

class alu {
  attr:
    float reg = 0.0;
    str op = '';

  state:
    bool reg_stored, op_executed, op_stored;

  func:
    get_reg ():float {ret reg;}

    store_reg (stack s){
        reg = float(s.get_value());
    }

    store_op(key k) {
        op = k.get_value();
    }

    exec_op(stack s) {
      float st1 = float(s.get_value());
      if (op == '+'){
        if (true){int d = 5;} # inner condition test
        reg += st1;
      } elif (op == '-'){
        reg -= st1;
      } elif (op == '*'){
        reg *= st1;
      } elif (op == '/') {
        reg /= st1;
      }
    }

  pred:
    is_op_executed() {ret op_executed;}
    is_op_stored() {ret op_stored;}
    is_reg_stored() {ret reg_stored;}

  oper:
    stack_to_register(key k, stack s){
      when:
        !k.processed &&
        k.is_op() &&
        !reg_stored &&
        is_op_stored()
      eff:
        int var1 = 0;  # test var declaration
        reg_stored = true;
        k.processed = true;
        s.tobe_cleaned = true;
      exec:
        s.store_reg();
    }

    store_op (key k){
      when:
        !is_op_stored() &&
        k.is_op() &&
        !reg_stored &&
        !k.processed
      eff:
        op_stored = true;
      exec:
        storeOp();
    }

    exec_op (key k, stack s) {
      when:
        is_op_stored() &&
        (k.is_op() || k.is_eq()) &&
        is_reg_stored() &&
        !k.processed &&
        !is_op_executed()
      eff:
        op_executed = true;
        if(k.is_eq() || k.is_op()) {
          op_stored = false;
        }
      exec:
        s.exec_op();
    }
}

class key {
  attr:
    str k = '';

  state:
    key_domain k_val;
    bool processed;

  dom:
    key_domain {digit, point, op, eq, clear, erase}

  init:
    key(str k1) {
      k = k1;
      if (k1.isdigit()) {
        k_val = digit;
      } elif (k1 == '.'){
        k_val = point;
      } elif (k1 == '=') {
        k_val = eq;
      } elif (k1 == 'c') {
        k_val = clear;
      } elif (k1 == 'r') {
        k_val = erase;
      } elif (k1 == '+' || k1 == '-' || k1 == '/' || k1 == '*'){
        k_val = op;
      }
      processed = false;
      updated = false;
    }

  func:
    get_value():str { ret k;}
    set_value(str val) { k = val;}

  pred:
    is_op() {ret (k_val == op);}
    is_digit() {ret (k_val == digit);}
    is_point() {ret (k_val == point);}
    is_eq() {ret (k_val == eq);}
    is_clear() {ret (k_val == clear);}
    is_erase() {ret (k_val == erase);}

  oper:
    no_op (stack s){
      when:
        !processed &&
        is_point() &&
        s.is_dec_point()
      eff:
        processed = true;
    }
}

class display {
  state:
    bool updated;

  func:
    show_stack(stack s){print(s.get_value());}

  oper:
    show_stack(stack s, key k) {
      when:
        !updated &&
        k.processed &&
        s.changed
      eff:
        updated = true;
        s.changed = false;
      exec:
        show_stack();
    }
}

class calculator {
  attr:
    stack st;
    display ds;
    alu al;

  goal:  # TODO: introduce functions either/any, all for goals
    process_key(key k){
      ret k.processed == true &&
          ds.updated == (st.changed ? true : False);
    }
}

main (str input_file) {
  calculator calc1;
  while (true){
    str key_val;
    # read stdin, analyze the key, assign its value
    key_val = input('enter here: ');
    if (key_val == 'q'){
      break;
    }
    calc1.process_key(key_val);
  }
}
